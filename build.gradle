plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version "1.5.5"
    id 'com.modrinth.minotaur' version '2.+'
}

defaultTasks 'build'

def buildNum = System.getenv('GITHUB_RUN_NUMBER') ?: 'SNAPSHOT'
project.group = 'net.pl3x.map.signs'
project.version = "$minecraftVersion-$buildNum"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    exclusiveContent {
        forRepository { maven { url = 'https://api.modrinth.com/maven/' } }
        filter { includeGroup 'maven.modrinth' }
    }
}

dependencies {
    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:$bukkitVersion")
    compileOnly "maven.modrinth:pl3xmap:$pl3xmapVersion"
}

base {
    archivesName = "${rootProject.name}-${project.name}"
}

assemble {
    dependsOn reobfJar
    doLast {
        delete fileTree("$buildDir").include('**/*-dev*.jar')
    }
}

reobfJar {
    outputJar = file("$buildDir/libs/${rootProject.name}-${project.version}.jar")
}

compileJava {
    options.encoding = 'UTF-8'
    options.release.set(17)
}

javadoc {
    options.encoding = 'UTF-8'
}

processResources {
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand(
                'name': rootProject.name,
                'group': project.group,
                'version': project.version,
                'authors': authors,
                'description': project.properties['description'],
                'website': website
        )
    }
}

modrinth {
    autoAddDependsOn = false
    token = System.getenv('MODRINTH_TOKEN')
    projectId = 'pl3xmap-signs'
    versionName = "${project.version}"
    versionNumber = "${project.version}"
    versionType = 'release'
    uploadFile = rootProject.layout.buildDirectory.file("libs/${rootProject.name}-${project.version}.jar").get()
    gameVersions.addAll(["${minecraftVersion}"])
    loaders.addAll(['bukkit', 'paper', 'purpur', 'spigot'])
    changelog = System.getenv('COMMIT_MESSAGE')
    dependencies {
        required.project "pl3xmap"
    }
}
